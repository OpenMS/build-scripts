CMAKE_MINIMUM_REQUIRED (VERSION 2.6)

## See https://itk.org/Bug/bug_relationship_graph.php?bug_id=9599&graph=dependency (to get rid of CMake warnings)
if(COMMAND CMAKE_POLICY) # CMake 2.4 does not have this command
  if(POLICY CMP0011)
    cmake_policy(SET CMP0011 NEW)
  endif(POLICY CMP0011)
endif(COMMAND CMAKE_POLICY)

# Verbosity (off for now)
set (CMAKE_VERBOSE_MAKEFILE OFF)

# Additional scripts:
# Path to look for additional scripts with macros.
# Defaults to directory of this script.
set (SCRIPT_PATH @script_path@)
if (NOT EXISTS ${SCRIPT_PATH})
	set (SCRIPT_PATH ${CMAKE_CURRENT_LIST_DIR})
endif()
# Loads general macros from the script dir.
if(NOT DEFINED TEST_MACROS_INCLUDED)
  include( "${SCRIPT_PATH}/global_macros.cmake" )
endif()


# Set if you have an optional compiler config cmake (old style)
set (COMPILER_CMAKE @compiler_cmake@)
# Looks for optional compiler definitions that hard code compiler setups. (Old style)
if (EXISTS "${COMPILER_CMAKE}")
  include("${COMPILER_CMAKE}" RESULT_VARIABLE COMPILER_CMAKE_INCLUDED)
elseif (EXISTS "${SCRIPT_PATH}/compiler.cmake")
  ## If the script path was not given or wrong, look in the standard script dir again
  include("${SCRIPT_PATH}/compiler.cmake" RESULT_VARIABLE COMPILER_CMAKE_INCLUDED)
endif()

# User variables, to be configured/replaced
# source and build_dir
set (CTEST_SOURCE_DIRECTORY @source_dir@ )
set (BUILD_DIRECTORY        @build_dir@ )
# general ctest/cmake/other programs
set (OPENMS_INSTALL_DIR @install_dir@)
# compiler/generator settings (ID is just for display)
set (GENERATOR @generator@ )
set (CONTRIB @contrib_dir@ )
set (COMPILER_IDENTIFIER @compiler_id@ )
# on unix you can specify the path to the compiler you want to use
# on windows, this will be determined from the Visual Studio command line
# environment that you used to call that script and the generator.
if(UNIX)
	set (C_COMPILER @c_compiler@ )
	set (CXX_COMPILER @cxx_compiler@ )
endif()
# contrib and thirdparty locations matching your compiler (check yourself)
set (QT_QMAKE_BIN_PATH @qmake_bin_path@ )
# description of this system (e.g. OS version: Win8, Ubuntu15.10)
set (SYSTEM_IDENTIFIER @system_identifier@ )
# usually the branch or other description about the state of the sources e.g. release2.0.0, develop
set (OPENMS_BUILDNAME_PREFIX @buildname_prefix@ )
# Describe type of build/tests e.g. Default, Release
set (BUILD_TYPE @build_type@ )
# site description of this machine e.g. scratchy.imp.fu-berlin.de
set (CTEST_SITE @ctest_site@ )
# if you want to submit to CDash at all. Might be helpful if you want to show results in other test dashboard
set (CDASH_SUBMIT @cdash_submit@)
# Path to a folder with all Thirdparty binaries in direct subfolders (for testing and packaging them)
set (THIRDPARTY_ROOT @thirdparty_root@)
# if generation of CTDs and therefore of the KNIME package sources should be enabled
set (KNIME_TEST @knime_test@ )
# More settings:
if (WIN32)
	# packaging on win is handled outside of cmake with standalone NSIS scripts
	set (PACKAGE_TEST Off )
else()
	set (PACKAGE_TEST @package_test@ )
endif()
set (BUILD_DOCU @build_docu@ )
set (BUILD_PYOPENMS @build_pyopenms@)
set (RUN_CHECKER @run_checker@ )
set (RUN_PYTHON_CHECKER @run_pychecker@ )
if (WIN32)
	set (TEST_COVERAGE Off )
else()
	set (TEST_COVERAGE @coverage_test@ )
endif()
set (EXTERNAL_CODE_TESTS @external_code_test@ )
set (NUMBER_THREADS @number_threads@)


# Git is actually not needed if you do not use ctest_update() but it should be present anyway
find_package(Git)
set (CTEST_GIT_COMMAND    "${GIT_EXECUTABLE}" )
# Setup important CTest variables (dependent on the ctest/cmake binary used to call this script)
set (CTEST_CMAKE_COMMAND  "${CMAKE_COMMAND}" )
set (CTEST_CTEST_COMMAND  "${CMAKE_CTEST_COMMAND}" )
set (CTEST_UPDATE_COMMAND "${CTEST_GIT_COMMAND}" )
set (CTEST_CHECK_HTTP_ERROR ON )


# Platform specific setup:
if(UNIX)
	if(COMPILER_CMAKE_INCLUDED)
		select_compiler(@compiler_id@)
	endif()
	# Warn if it is not Makefiles or XCode
	if (NOT GENERATOR MATCHES "Unix Makefiles")
	    if (APPLE)
	        if (NOT GENERATOR MATCHES "XCode")
		    message(FATAL_ERROR "Only XCode or Unix Makefiles supported on Mac")
		endif()
            else()
	        message(FATAL_ERROR "Only Unix Makefiles supported on Linux")
	    endif()
	endif()
elseif(WIN32)
	# Disable certain tests if falsely enabled. TODO Raise warning?
	# packaging tests only allowed for UNIX (lnx,osx), otherwise we use NSIS Installer scripts for Win
	set (PACKAGE_TEST Off )
	# Standard uses gcov. Difficult on windows.
	set (TEST_COVERAGE Off )

	# Overrides or initially defines compiler settings
	if(COMPILER_CMAKE_INCLUDED)
		select_vs_version(@vs@ @arch@)
	else()
		# check if generator is VS (e.g., Visual Studio 10 Win64)
		if (NOT GENERATOR MATCHES "Visual Studio*")
		    message(FATAL_ERROR "Only Visual Studio supported on Windows")
		endif()
	endif()
endif()

# Next steps, found in test_macros.cmake
prepare_notes()
run_tests()
